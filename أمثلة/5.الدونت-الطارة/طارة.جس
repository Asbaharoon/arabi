// إذا أردت فهم المعادلات قم بقراءة المقالة التي كتبها صاحب الكود الأصلي المكتوب بلغي السي
// المقالة: https://www.a1k0n.net/2011/07/20/donut-math.html

ثابت آنسي = استدعي('هروب-الآنسي')؛

ثابت إزاحة_ثيتا = 0.07؛
ثابت إزاحة_فاي = 0.02؛
ثابت إزاحة_أ = 0.02؛
ثابت إزاحة_ب = 0.05؛
ثابت التأخر = 10؛ // مقدار  التأخر بين تصيير إطارين متتاليين

ثابت نق1 = 1؛
ثابت نق2 = 2؛
// هذا الرقم يحدد عمق أو بعد الجسم في الاتجاه الداخل للشاشة
// لذا عندما يزداد يقل الحجم، ويجب مراعاة عدم خروح الشكل عن الحافة
ثابت ك2 = 10؛


ثابت حروف_الإضاءة = ".,-~:;=!*#$@"؛

ثابت اتساع_الشاشة = عملية.خارج.أعمدة؛
ثابت ارتفاع_الشاشة = عملية.خارج.صفوف؛

// يجب مراعاة أن تكون الطرفية مربعة حتى لا تصبح الدائرة مُدَحّاة على شكل إهليج
ثابت ك_س = اتساع_الشاشة*ك2*3/(8*(نق1+نق2))؛
ثابت ك_ص = ارتفاع_الشاشة*ك2*3/(8*(نق1+نق2))؛

دالة تصيير_إطار (أ، ب) {

	// عملية.خارج.اكتب(آنسي.احفظ_موضع_المؤشر)؛

	ثابت جتا_أ = رياضيات.جتا(أ)، جا_أ = رياضيات.جا(أ)؛
	ثابت جتا_ب = رياضيات.جتا(ب)، جا_ب = رياضيات.جا(ب)؛

	// [0..اتساع_الشاشة، 0..ارتفاع_الشاشة]
	ثابت خارج = انشئ مصفوفة(ارتفاع_الشاشة).املئ().خرط(()=> انشئ مصفوفة(اتساع_الشاشة).املئ(' '))؛
	ثابت مخزن = انشئ مصفوفة(ارتفاع_الشاشة).املئ().خرط(()=> انشئ مصفوفة(اتساع_الشاشة).املئ(0))؛

	لكل(عرف ثيتا=0؛ ثيتا<2*رياضيات.ط؛ ثيتا+=إزاحة_ثيتا) {
  
		عرف جا_ثيتا = رياضيات.جا(ثيتا)، جتا_ثيتا = رياضيات.جتا(ثيتا)؛
  		متغير س_الدائرة = نق2 + نق1*جتا_ثيتا؛
		متغير ص_الدائرة = نق1*جا_ثيتا؛

		لكل(عرف فاي=0؛ فاي<2*رياضيات.ط؛ فاي+=إزاحة_فاي) {

			متغير جا_فاي = رياضيات.جا(فاي)، جتا_فاي = رياضيات.جتا(فاي)؛

			متغير س = س_الدائرة*(جتا_ب*جتا_فاي + جا_أ*جا_ب*جا_فاي) -
				ص_الدائرة * جتا_أ * جا_ب؛
			متغير ص = س_الدائرة*(جا_ب*جتا_فاي - جا_أ*جتا_ب*جا_فاي) +
				ص_الدائرة * جتا_أ * جتا_ب؛
			متغير ع = ك2 + جتا_أ*س_الدائرة*جا_فاي + ص_الدائرة*جا_أ؛

			متغير مضع = 1/ع؛ // المعكوس الضربي لـ "ع"

			متغير إسقاط_س = ~~(اتساع_الشاشة/2 + ك_س*مضع*س)؛ // ~~ لجعله رقما صحيحا
			متغير إسقاط_ص = ~~(ارتفاع_الشاشة/2 - ك_ص*مضع*ص)؛ // ~~ لجعله رقما صحيحا

			متغير إضاءة = جتا_فاي*جتا_ثيتا*جا_ب - جتا_أ*جتا_ثيتا*جا_فاي -
				جا_أ*جا_ثيتا + جتا_ب*(جتا_أ*جا_ثيتا - جتا_ثيتا*جا_أ*جا_فاي)؛
			لو (إضاءة > 0) {
				لو(إسقاط_ص >= ارتفاع_الشاشة || إسقاط_ص < 0) {
					لوحة.جدول({ إسقاط_س، إسقاط_ص، اتساع_الشاشة، ارتفاع_الشاشة })؛
					عملية.اخرج()؛
				}
				وإلا لو (مضع > مخزن[إسقاط_ص][إسقاط_س]) {
					مخزن[إسقاط_ص][إسقاط_س] = مضع؛
					// |0 لجعله رقما صحيحا
					عرف دليل_الحرف = (إضاءة*8) | 0؛
					لو (!حروف_الإضاءة[دليل_الحرف]) {
						لوحة.جدول({ دليل_الحرف، إضاءة })؛
						عملية.اخرج()؛
					}
					خارج[إسقاط_ص][إسقاط_س] = حروف_الإضاءة[دليل_الحرف]؛
				}
			}

		}

	}


	// لكتابة لـ process.stdout

	عرف الطارة = خارج.خرط(س=>س.ادمج("")).ادمج("\n")؛
	عملية.خارج.اكتب(الطارة)؛
	عملية.خارج.اكتب("\x1b[H")؛
	// عملية.خارج.اكتب(آنسي.اعد_المؤشر_لموضعه)؛

	ضع_مؤقتا(()=>تصيير_إطار(أ+إزاحة_أ، ب+إزاحة_ب)، التأخر)؛

}


تصيير_إطار(رياضيات.ط/3، رياضيات.ط/3)؛


